'DEBPROC' COPYTAB  tab1*'TABLE' ;

tabco = 'TABLE';

ref = 'INDEX' tab1;
nref = 'DIME' ref;
'SI' ('NEG' nref 0);
  'REPETER' bouc nref;
    tk = TYPE  ref. &bouc;
    'SI' ('EGA' tk 'ENTIER');
      k = ref . &bouc;
    'SINON';
      k = 'CHAINE' ref . &bouc; 
    'FINSI';
    tk = 'TYPE' tab1. k;
    'SI' ('EGA' tk 'TABLE');
      tabco. k = COPYTAB (tab1 . k);
    'SINON';
      tabco. k = tab1 . k;
    'FINSI';
  'FIN' bouc;
'FINSI';
'FINPROC' tabco;

* Maillage simplifie d'un 1/4  eprouvette AE10-6
***********************************************

'OPTION' 'ERREUR' 'FATALE' ;
'OPTION' 'DIME' 2 'ELEM' QUA8;
'OPTION' 'MODELISER' 'PLAN' 'DEFO';
'OPTION' 'EPSILON' 'UTILISATEUR';
                                                                    
* definition des points                                                 
p0 = (0.     0.);
p1 = (3.e-3  0.);
p2 = (5.4e-3 4.8e-3);
p4 = (0.     4.8e-3);

c  = (9.e-3  0.);
                                                                      
* Definition des lignes                                                 

CO   = 'CERCLE' 26 p1 c p2;
R    = 'NOMC'   ('COORDONNEE' 1 CO) 'UX';
lAI1 = CO 'MOINS' R;

stot1 = 'REGLER' 26 CO LAI1;
LRD   = 'FACE' 4 stot1;
LRM   = 'FACE' 2 stot1;
y    = 'NOMC'   ('COORDONNEE' 2 LRM) 'UY';
LRU  = (LRM 'MOIN' y) 'PLUS' (0. 30e-3);

d = 5.4e-3 '/' 26;
stot2 =  LRM 'REGLER' 'DINI' d 'DFIN'  ((30e-3/4.8e-3) '*' d)  LRU;

stot = stot1 'ET' stot2;

*
* Matériau
*

coel2D = 'MOTS' 'YOUN' 'NU' 'RHO' 'ALPH';
stav2D = 'MOTS'  'EERR' 'EEZZ' 'EETT' 'EERZ' 'P';
para2D = 'MOTS' 'T';

mod1 = 'MODELISER' stot 'MECANIQUE' 'ELASTIQUE' 'ISOTROPE'
   'NON_LINEAIRE' 'UTILISATEUR' 'EPSILON' 'UTILISATEUR' Q8RI
   'LIB_LOI' './src/libUmatBehaviour.so'
   'FCT_LOI' 'umatlogarithmicstrainplasticity_malls'
   'C_MATERIAU' coel2D
   'C_VARINTER' stav2D
   'PARA_LOI'   para2D
   'CONS' M;
   
MAT1 = 'MATERIAU' MOD1 'YOUN' 200e9 'NU' 0.3 'ALPH' 0. 'RHO' 0.;


*** DISCRETISATION TEMPORELLE
TMAX = 1.;
NPAS = 50;

LIT1 = PROG 0. PAS (TMAX/ NPAS)  TMAX;

*** CONDITIONS AUX LIMITES
************************************************
X = 'COORDONNEE' 1 stot;
LRG = X 'POINT' 'COMPRIS' -1.e-12 1.e-12;

CL1 = 'BLOQUE' 'UY'  LRD ;
CL2 = 'BLOQUE' 'UY'  LRU ;
CL3 = 'BLOQUE' 'UX'  LRG ;
CLE1 = CL1 'ET' CL2 'ET' CL3;

* Chargement mécanique et thermique
UMAX = 6.e-3;
LI1 = PROG 0. TMAX ;
LI2 = PROG 0. 1. ;
EV = EVOL MANU T LI1 F(T) LI2 ;
DEP1 = DEPI CL2 UMAX;
CHA1 = CHAR 'DIMP' DEP1 EV ;

* Champ de température
TEK = 293.15;
THE1 = MANU 'CHPO' stot 1 'T' TEK ;
EV2 = EVOL MANU (prog 0. TMAX) (prog 1. 1.) ;
CHARTHER = CHAR 'T' THE1 EV2 ;
                             
** CALCUL
* Définition des pas de calcul et de sauvegarde
************************************************************
* Définition de la table de la phase de charge
TAB1 = TABLE ;
TAB1.'MODELE' = MOD1 ;
TAB1.'MATERIAUX' = MAT1 ;        
TAB1.'BLOCAGES_MECANIQUES' = CLE1 ;
TAB1.'CHARGEMENT' = CHA1 'ET' CHARTHER ;
TAB1.'VARIINT_INI' = (ZERO MOD1 'VARINTER');
TAB1.'GRANDS_DEPLACEMENTS' = VRAI ;
TAB1.'MESSAGES' = VRAI ;
TAB1. '__FNONL__'         = 'FAUX';
TAB1 . 'K_TANGENT'           = 'KTAC';
TAB1 . 'INCREPL_VERSION'     = 4;

NB1      = 100000000;
t   = 'EXTR' LIT1 1;
te  = 'EXTR' LIT1 (DIME LIT1);
dt  = ('EXTR' LIT1 2) '-' t;
p = 0;
u = 'PROGRESSION';
Fi = 'PROGRESSION';
'REPE' B1 NB1 ;
  'SI' ( '<' (ABS (te - t) ) (dt/2));
    'QUITTER' B1;
  'FINSI';
  'MESSAGE' 'time: from ' t ' to ' (t+dt);
  TAB1. 'TEMPS_INI'   = t;
  'SI' ('>' (t+dt) te);
    TAB1 . 'TEMPS_FIN' = te ;
  'SINO' ;
    TAB1 . 'TEMPS_FIN' = t+dt ;
  'FINSI';
  STAB1 = INCREPL TAB1 ;
*  'OUBLIER' tab1 'GRADF_INI';
*  'OUBLIER' tab1 '__PL_M_INIT__';  
  bconv = FAUX;
  'SI' ('EXISTE' stab1 'CONV_MECA');
    bconv = stab1.'CONV_MECA';
  'FINSI' ;
  'SI' ('EXISTE'  stab1 '_ERREUR_');
       bCONV = bCONV 'ET' ('EGA' (STAB1. '_ERREUR_') 0);
  'FINSI' ;
  'SI' bCONV ;
    u = u 'ET' ('PROGRESSION' (UMAX '*' ((t'+'dt) '/' TMAX)));
    r = 'MAXIMUM' ('RESULT' ('REACTION' cl2 (stab1. 'LX_FIN')));
    Fi = Fi 'ET' ('PROGRESSION' r);
    p = p+1;
    t = t+dt;
    'SI'('>' t te);
      t = te;
    'FINSI' ;
*    'MESSAGE' 'DT_SUIV: ' ('MINIMUN' (STAB1. 'DT_SUIV'));
*    dt = ('MINIMUM' ('PROG' ('MINIMUN' (STAB1. 'DT_SUIV')) 1.2))*dt;
  'SINON';
    'ERREUR'  'NON CONVERGENCE';  
  'FINSI' ;
*                                                                     *
'FIN' B1 ;

ev = 'EVOLUTION' 'MANUEL' u Fi;
'LIST' ev;

d  = tab1. 'U_FIN';
ur = ('EXTRAIRE' d 'UR' p1);

REA1 = 'REACTION' cl2 (tab1. 'LX_FIN');
rcl2 = ('MAXIMUM' ('RESULT' REA1));

'MESSAGE' ur;
'MESSAGE' rcl2;

** Comparaisons à des résultats de référence
OK = VRAI;

urref =-8.55947e-4;
rclref= 2.08080E+05;

'SI' ('>' ('ABS' (rcl2 '-' rclref)) 1.);
   'MESSAGE' 'ERREUR SUR LA REACTION (' rcl2 ' vs ' rclref ')';
   OK = FAUX;
'FINSI';
'SI' ('>' ('ABS' (ur '-' urref)) 1.e-8);
   'MESSAGE' 'ERREUR SUR LE DEPLACEMENT (' ur ' vs ' urref ')';
   OK = FAUX;
'FINSI';

'SI' ('NON' OK);
  'MESS' ' ---------------------' ;
  'MESS' '  ECHEC DU CAS-TEST !' ;
  'MESS' ' ---------------------' ;
  'ERREUR' 5 ;
'SINON' ;
  'MESS' ' ----------------------' ;
  'MESS' '  SUCCES DU CAS-TEST !' ;
  'MESS' ' ----------------------' ;
'FINSI' ;
'SAUTER' 1 'LIGNE' ;

'FIN';
